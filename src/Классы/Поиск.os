Перем ВидПоиска Экспорт;

Процедура ПриСозданииОбъекта()
	
	ВидПоиска = 1;//ВидыПоиска.ПоискЗначения;

КонецПроцедуры

Функция СимволСтроки(Строка, Номер) Экспорт
	Возврат Сред(Строка, Номер, 1);
КонецФункции

Функция Растояние(Источник, Приемник) Экспорт

	Если ПустаяСтрока(Источник) Тогда
		Если ПустаяСтрока(Приемник) Тогда
			Возврат 0;
		КонецЕсли;
		Возврат СтрДлина(Приемник); 
	КонецЕсли;

	ДлинаПриемника = СтрДлина(Приемник);
	ДлинаИсточника = СтрДлина(Источник);

	ТаблицаРасстояний = Новый Матрица(2, ДлинаПриемника+1);

	Для Сч = 1 по ДлинаПриемника Цикл
		ТаблицаРасстояний.Значение(0, Сч, Сч);
	КонецЦикла;

	ТекущаяСтрока = 0;
	
	Для СчИсточника = 1 по ДлинаИсточника Цикл
		ТекущаяСтрока = ПобитовоеИ(СчИсточника, 1);
		ТаблицаРасстояний.Значение(ТекущаяСтрока, 0, СчИсточника);
		ПредыдущаяСтрока = ПобитовоеИсключительноеИли(ТекущаяСтрока, 1);
		Для СчПриемника = 1 По ДлинаПриемника Цикл
			Вес = ?(СимволСтроки(Приемник , СчПриемника) = СимволСтроки(Источник, СчИсточника), 0, 1);	
			
			РасчетноеРастояние = МИН(
				МИН(
					ТаблицаРасстояний.Значение(ПредыдущаяСтрока, СчПриемника) + 1,
					ТаблицаРасстояний.Значение(ТекущаяСтрока, СчПриемника - 1) +1
					),			
				ТаблицаРасстояний.Значение(ПредыдущаяСтрока, СчПриемника - 1) + Вес
			);
			ТаблицаРасстояний.Значение(ТекущаяСтрока, СчПриемника, РасчетноеРастояние);
		КонецЦикла;

	КонецЦикла;

	Возврат ТаблицаРасстояний.Значение(ТекущаяСтрока, ДлинаПриемника);
	
КонецФункции